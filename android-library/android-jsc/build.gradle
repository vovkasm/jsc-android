plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def rootDir = "${rootProject.projectDir}/.."
def distDir = "${rootDir}/dist"
def jniLibsDir = "${rootDir}/build/output/lib"

android {
    namespace = "org.webkit.androidjsc"
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        aarMetadata {
            minCompileSdk 31
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["${jniLibsDir}"]
        }
    }

    androidResources {
        noCompress 'dat' // Disable compression of icudatXXl.dat file
    }

    packagingOptions {
//        jniLibs.keepDebugSymbols += "**/libjsc.so"
    }

    publishing {
        singleVariant('release')
    }
}

dependencies {}

tasks.register('copyHeadersToDist', Copy) {
    description = 'Copy include headers into dist dir'
    from "${rootDir}/build/output/include/JavaScriptCore"
    into "${distDir}/include"
}
tasks.register('publishDist') {
    dependsOn = ['copyHeadersToDist', 'publishReleasePublicationToDistRepository']
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'org.webkit'
                artifactId = 'android-jsc'
                version = 'r1'
            }
        }
        repositories {
            maven {
                name = 'dist'
                url = distDir
            }
        }
    }
}
